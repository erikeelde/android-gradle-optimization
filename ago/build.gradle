plugins {
    id "java"
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.10.1"
    id "org.jetbrains.kotlin.jvm" version "1.3.31"
    id "maven-publish"
    id "signing"
}

version "unspecified"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

apply from: "../ktlint.gradle"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation gradleApi()

    testImplementation "junit:junit:4.12"
    testImplementation gradleTestKit()
    testImplementation "com.google.truth:truth:0.44"
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")

    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

// Use java-gradle-plugin to generate plugin descriptors and specify plugin ids
gradlePlugin {
    plugins {
        agoPlugin {
            id = 'se.eelde.ago'
            implementationClass = 'se.eelde.ago.AgoPlugin'
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'http://www.eelde.se/'
    vcsUrl = 'https://github.com/erikeelde/android-gradle-optimization'
    description = 'A plugin that verifies that optimizations are enabled on development machines.'
    tags = ['optimization']

    plugins {
        agoPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle optimizations plugin'
            version = '0.1'
        }
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            groupId = 'se.eelde'
            artifactId = project.name
            version = 1 + "-SNAPSHOT"

            from components.java

            artifact sourceJar
            artifact javadocJar
        }
    }
}

